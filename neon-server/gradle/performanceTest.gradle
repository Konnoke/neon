/*
 * Copyright 2013 Next Century Corporation
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

import org.apache.tools.ant.filters.ReplaceTokens
import org.gradle.api.plugins.jetty.internal.Monitor

apply plugin: 'scala'
apply plugin: 'jetty'

def concurrencyTestSourceDir = 'src/concurrencyTest/scala'
def concurrencyTestPort = 11402
def TEST_GROUP = JavaBasePlugin.VERIFICATION_GROUP

sourceSets {
    concurrencyTest {
        scala.srcDir file(concurrencyTestSourceDir)
        compileClasspath = configurations.concurrencyTestCompile
        runtimeClasspath = output + compileClasspath + configurations.concurrencyTestRuntime
    }
}

repositories {
    mavenCentral()
    maven { url "http://repository.excilys.com/content/groups/public" }
}

configurations {
    concurrencyTestCompile
    concurrencyTestRuntime { extendsFrom concurrencyTestCompile }
}

dependencies {
    compile "org.scala-lang:scala-library:$scalaLibraryVersion"
    concurrencyTestCompile "com.excilys.ebi.gatling.highcharts:gatling-charts-highcharts:$gatlingVersion"
    concurrencyTestRuntime "com.excilys.ebi.gatling:gatling-app:$gatlingVersion"
}

// This is the standard way to run gatling from gradle, see https://github.com/vincentkok/gradle-gatling
task gatling {
    dependsOn 'checkDbProperties', 'concurrencyTestWar', 'insertDataIntoMongo', 'insertDataIntoSparkSQL'
    group = TEST_GROUP

    doLast {
        sourceSets.concurrencyTest.output.classesDir.eachFileRecurse { file ->
            if(!file.isFile() || containsExcludedNames(file)){
                return
            }
            def gatlingScenarioClass = getFullyQualifiedClassName(file)

            javaexec {
                main = 'com.excilys.ebi.gatling.app.Gatling'
                classpath = sourceSets.concurrencyTest.output + sourceSets.concurrencyTest.runtimeClasspath
                systemProperties = createSystemProperties()
                args '-sbf',
                        sourceSets.concurrencyTest.output.classesDir,
                        '-s',
                        gatlingScenarioClass,
                        '-rf',
                        'build/reports/gatling'
            }
        }
    }
}

def containsExcludedNames(File file){
    def excludes = ["Headers", "Responses", "Requests"]
    boolean excludeFound = false
    excludes.each {
        if(file.name.contains(it)){
            excludeFound = true
        }
    }
    return excludeFound
}

def getFullyQualifiedClassName(File file){
    (file.getPath() - (sourceSets.concurrencyTest.output.classesDir.getPath() + File.separator) - '.class').replace(File.separator, '.')
}

def createSystemProperties(){
    return [
        "mongo.host" : getMongoHost(),
        "sparksql.host" : getSparkSQLHost()
    ]
}

task checkDbProperties << {
    if (!project.ext.has('mongo.host')) {
        throw new InvalidUserDataException("The 'mongo.host' property must be specified for gatling builds")
    }
    if (!project.ext.has('hdfs.url')) {
        throw new InvalidUserDataException("The 'hdfs.url' property must be specified for gatling builds")
    }
    if (!project.ext.has('sparksql.host')) {
        throw new InvalidUserDataException("The 'sparksql.host' property must be specified for gatling builds")
    }
}

task concurrencyTestWar(type: org.gradle.api.plugins.jetty.JettyRunWar) {
    description = "Launches an instance of neon used for concurrency tests"
    dependsOn 'concurrencyTestClasses'
    daemon = true
    httpPort = concurrencyTestPort
    stopPort = httpPort + 2
    stopKey = 'stop'
}

task insertDataIntoMongo(type: com.ncc.neon.data.MongoDataInserter) {
    description = "Inserts data into mongo used by concurrency tests"
    dependsOn 'deleteDataFromMongoBeforeInsert', 'generateMongoJson'
    host = getMongoHost()
}
task deleteDataFromMongoBeforeInsert(type: com.ncc.neon.data.MongoDataDeleter) {
    description = "Deletes any old concurrency test data from mongo that may have been left around by previous tests"
    host = getMongoHost()
}

task insertDataIntoSparkSQL(type: com.ncc.neon.data.SparkSQLDataInserter) {
    description = "Inserts data into spark sql used by concurrency tests"
    dependsOn 'deleteDataFromSparkSQLBeforeInsert', 'generateSparkSQLCSV', 'generateSparkSQLJson'
    host = getSparkSQLHost()
    hdfsUrl = project.getHdfsUrl()
}
task deleteDataFromSparkSQLBeforeInsert(type: com.ncc.neon.data.SparkSQLDataDeleter) {
    description = "Deletes any old concurrent tests data from spark sql that may have been left around by previous tests"
    host = getSparkSQLHost()
}

task deleteDataFromMongo(type: com.ncc.neon.data.MongoDataDeleter) {
    description = "Deletes any concurrency test data from mongo"
    host = getMongoHost()
}

task deleteDataFromSparkSQL(type: com.ncc.neon.data.SparkSQLDataDeleter) {
    description = "Deletes any concurrency test data from spark sql"
    host = getSparkSQLHost()
}

task afterGatlingTest{
    description = "Cleans up any concurrency test data"
    // Tasks that are used for "finalizedBy" are still shown in './gradlew tasks' (GRADLE-2949), so put it in the correct group
    group = TEST_GROUP
    dependsOn 'stopGatlingJetty', 'deleteDataFromMongo', 'deleteDataFromSparkSQL'
}

gatling.finalizedBy afterGatlingTest

task stopGatlingJetty(type: org.gradle.api.plugins.jetty.JettyStop) {
    description = "Stops the instance of neon used for concurrency testing"
    stopKey = 'stop'
    stopPort = concurrencyTestPort + 2
}
