/*
 * Copyright 2013 Next Century Corporation
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

import org.apache.tools.ant.filters.ReplaceTokens
import org.gradle.api.plugins.jetty.internal.Monitor

apply plugin: 'scala'
apply plugin: 'org.akhikhl.gretty' // Replacement for default jetty plugin

def performanceTestSourceDir = 'src/performanceTest/scala'
def performanceTestPort = 11402
def TEST_GROUP = JavaBasePlugin.VERIFICATION_GROUP

sourceSets {
    performanceTest {
        scala.srcDir file(performanceTestSourceDir)
        compileClasspath = configurations.performanceTestCompile
        runtimeClasspath = output + compileClasspath + configurations.performanceTestRuntime
    }
}

repositories {
    mavenCentral()
    maven { url "http://repository.excilys.com/content/groups/public" }
}

configurations {
    performanceTestCompile
    performanceTestRuntime { extendsFrom performanceTestCompile }
}

dependencies {
    compile "org.scala-lang:scala-library:$scalaLibraryVersion"
    performanceTestCompile "com.excilys.ebi.gatling.highcharts:gatling-charts-highcharts:$gatlingVersion"
    performanceTestRuntime "com.excilys.ebi.gatling:gatling-app:$gatlingVersion"
}

// This is the standard way to run gatling from gradle, see https://github.com/vincentkok/gradle-gatling
task performanceTest {
    dependsOn 'checkDbProperties', 'performanceTestWar', 'insertDataIntoMongo', 'insertDataIntoSparkSQL'
    group = TEST_GROUP

    doLast {
        sourceSets.performanceTest.output.classesDir.eachFileRecurse { file ->
            if(!file.isFile() || containsExcludedNames(file)){
                return
            }
            def gatlingScenarioClass = getFullyQualifiedClassName(file)

            javaexec {
                main = 'com.excilys.ebi.gatling.app.Gatling'
                classpath = sourceSets.performanceTest.output + sourceSets.performanceTest.runtimeClasspath
                systemProperties = createSystemProperties()
                args '-sbf',
                        sourceSets.performanceTest.output.classesDir,
                        '-s',
                        gatlingScenarioClass,
                        '-rf',
                        'build/reports/gatling'
            }
        }
    }
}

def containsExcludedNames(File file){
    def excludes = ["Headers", "Responses", "Requests"]
    boolean excludeFound = false
    excludes.each {
        if(file.name.contains(it)){
            excludeFound = true
        }
    }
    return excludeFound
}

def getFullyQualifiedClassName(File file){
    (file.getPath() - (sourceSets.performanceTest.output.classesDir.getPath() + File.separator) - '.class').replace(File.separator, '.')
}

def createSystemProperties(){
    return [
        "mongo.host" : getMongoHost(),
        "sparksql.host" : getSparkSQLHost()
    ]
}

task checkDbProperties << {
    if (!project.ext.has('mongo.host')) {
        throw new InvalidUserDataException("The 'mongo.host' property must be specified for performanceTest builds")
    }
    if (!project.ext.has('hdfs.url')) {
        throw new InvalidUserDataException("The 'hdfs.url' property must be specified for performanceTest builds")
    }
    if (!project.ext.has('sparksql.host')) {
        throw new InvalidUserDataException("The 'sparksql.host' property must be specified for performanceTest builds")
    }
}

task performanceTestWar(type: org.gradle.api.plugins.jetty.JettyRunWar) {
    description = "Launches an instance of neon used for performance tests"
    dependsOn 'performanceTestClasses'
    daemon = true
    httpPort = performanceTestPort
    stopPort = httpPort + 2
    stopKey = 'stop'
}

task insertDataIntoMongo(type: com.ncc.neon.data.MongoDataInserter) {
    description = "Inserts data into mongo used by performance tests"
    dependsOn 'deleteDataFromMongoBeforeInsert', 'generateMongoJson'
    host = getMongoHost()
}
task deleteDataFromMongoBeforeInsert(type: com.ncc.neon.data.MongoDataDeleter) {
    description = "Deletes any old performance test data from mongo that may have been left around by previous tests"
    host = getMongoHost()
}

task insertDataIntoSparkSQL(type: com.ncc.neon.data.SparkSQLDataInserter) {
    description = "Inserts data into spark sql used by performance tests"
    dependsOn 'deleteDataFromSparkSQLBeforeInsert', 'generateSparkSQLCSV', 'generateSparkSQLJson'
    host = getSparkSQLHost()
    hdfsUrl = project.getHdfsUrl()
}
task deleteDataFromSparkSQLBeforeInsert(type: com.ncc.neon.data.SparkSQLDataDeleter) {
    description = "Deletes any old performance tests data from spark sql that may have been left around by previous tests"
    host = getSparkSQLHost()
}

task deleteDataFromMongo(type: com.ncc.neon.data.MongoDataDeleter) {
    description = "Deletes any performance test data from mongo"
    host = getMongoHost()
}

task deleteDataFromSparkSQL(type: com.ncc.neon.data.SparkSQLDataDeleter) {
    description = "Deletes any performance test data from spark sql"
    host = getSparkSQLHost()
}

task afterPerformanceTest{
    description = "Cleans up any performance test data"
    // Tasks that are used for "finalizedBy" are still shown in './gradlew tasks' (GRADLE-2949), so put it in the correct group
    group = TEST_GROUP
    dependsOn 'stopGatlingJetty', 'deleteDataFromMongo', 'deleteDataFromSparkSQL'
}

performanceTest.finalizedBy afterPerformanceTest

task stopGatlingJetty(type: org.gradle.api.plugins.jetty.JettyStop) {
    description = "Stops the instance of neon used for performance testing"
    stopKey = 'stop'
    stopPort = performanceTestPort + 2
}
