/*
 * Copyright 2013 Next Century Corporation
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

// applies the jdepend plugin to a build and will fail the build if there are any package cycles
apply plugin: 'jdepend'

jdepend {
    sourceSets = [sourceSets.main]
}

jdependMain.doLast {
    File file = new File(jdepend.reportsDir, "main.xml");
    def cycleCount = new XmlSlurper().parse(file).Cycles.Package.size()
    if (cycleCount > 0) {
        throw new GradleException("Package cycles exist. See JDepend report at ${file}")
    }
}

// configure jdepend tasks to be in the correct task group
sourceSets.each { sourceSet ->
    def name = sourceSet.name
    // this set should only have one element for each source set
    def jdependTasks = project.getTasksByName("jdepend${name.capitalize()}", false)
    jdependTasks.each { task ->
        task.group = JavaBasePlugin.VERIFICATION_GROUP
    }
}
