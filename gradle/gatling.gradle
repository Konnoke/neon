/*
 * Copyright 2013 Next Century Corporation
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'scala'
apply plugin: 'jetty'

def concurrencyTestSourceDir = 'src/concurrencyTest/scala'
def concurrencyTestPort = 11402
def GATLING_GROUP = "Gatling"

repositories {
    mavenCentral()
    maven { url "http://repository.excilys.com/content/groups/public" }
}

configurations {
    concurrencyTestCompile
    concurrencyTestRuntime { extendsFrom concurrencyTestCompile }
}

sourceSets {
    concurrencyTest {
        scala.srcDir file(concurrencyTestSourceDir)
        compileClasspath = configurations.concurrencyTestCompile
        runtimeClasspath = output + compileClasspath + configurations.concurrencyTestRuntime
    }
}

dependencies {
    compile 'org.scala-lang:scala-library:2.10.3'
    concurrencyTestCompile("com.excilys.ebi.gatling.highcharts:gatling-charts-highcharts:1.5.3")
    concurrencyTestRuntime "com.excilys.ebi.gatling:gatling-app:1.5.3"
}

task gatling {
    dependsOn 'concurrencyTestWar', 'insertDataIntoMongo', 'insertDataIntoHive'
    group = GATLING_GROUP

    doLast {
        sourceSets.concurrencyTest.output.classesDir.eachFileRecurse { file ->
            if(!file.isFile() || containsExcludedNames(file)){
                return
            }
            def gatlingScenarioClass = getFullyQualifiedClassName(file)

            javaexec {
                main = 'com.excilys.ebi.gatling.app.Gatling'
                classpath = sourceSets.concurrencyTest.output + sourceSets.concurrencyTest.runtimeClasspath
                systemProperties = createSystemProperties()
                args '-sbf',
                        sourceSets.concurrencyTest.output.classesDir,
                        '-s',
                        gatlingScenarioClass,
                        '-rf',
                        'build/reports/gatling'
            }
        }
    }
}

def containsExcludedNames(File file){
    def excludes = ["Headers", "Responses", "Requests"]
    boolean excludeFound = false
    excludes.each {
        if(file.name.contains(it)){
            excludeFound = true
        }
    }
    return excludeFound
}

def getFullyQualifiedClassName(File file){
    (file.getPath() - (sourceSets.concurrencyTest.output.classesDir.getPath() + File.separator) - '.class').replace(File.separator, '.')
}

def createSystemProperties(){
    return [
        "mongo.hosts" : getMongoHosts(),
        "hive.host" : getHiveHost()
    ]
}

task copyConcurrencyWebapp(type: Copy) {
    from 'src/concurrencyTest/webapp'
    into "${buildDir}/tmp/concurrencyRecorder/webapp"
}

task concurrencyRecorder(type: org.gradle.api.plugins.jetty.JettyRunWar) {
    dependsOn 'clean', 'concurrencyTestClasses', 'copyConcurrencyWebapp'
    group = GATLING_GROUP
    daemon = false
    httpPort = concurrencyTestPort
}

task concurrencyTestWar(type: org.gradle.api.plugins.jetty.JettyRunWar) {
    dependsOn 'concurrencyTestClasses'
    group = GATLING_GROUP
    daemon = true
    httpPort = concurrencyTestPort
    stopPort = httpPort + 2
    stopKey = 'stop'
    jettyConfig = new File(sourceSets.concurrencyTest.output.resourcesDir, 'jetty-config.xml')

}

processConcurrencyTestResources {
    from(sourceSets.concurrencyTest.resources.srcDirs) {
        filter(ReplaceTokens, tokens: [
                "mongo.hosts": getMongoHosts()
        ])
    }
}

task insertDataIntoMongo(type: com.ncc.neon.data.MongoDataInserter) {
    dependsOn 'deleteDataFromMongoBeforeInsert', 'generateMongoJson'
    host = getMongoHosts()
}
task deleteDataFromMongoBeforeInsert(type: com.ncc.neon.data.MongoDataDeleter) {
    host = getMongoHosts()
}

task insertDataIntoHive(type: com.ncc.neon.data.HiveDataInserter) {
    dependsOn 'deleteDataFromHiveBeforeInsert', 'generateHiveCSV', 'generateHiveJson'
    host = getHiveHost()
    hdfsUrl = getHdfsUrl()
}
task deleteDataFromHiveBeforeInsert(type: com.ncc.neon.data.HiveDataDeleter) {
    host = getHiveHost()
}

task deleteDataFromMongo(type: com.ncc.neon.data.MongoDataDeleter) {
    host = getMongoHosts()
}

task deleteDataFromHive(type: com.ncc.neon.data.HiveDataDeleter) {
    host = getHiveHost()
}

task afterGatlingTest{
    group = GATLING_GROUP
    dependsOn 'stopGatlingJetty', 'deleteDataFromMongo', 'deleteDataFromHive'
}

gatling.finalizedBy afterGatlingTest

task stopGatlingJetty(type: org.gradle.api.plugins.jetty.JettyStop) {
    group = GATLING_GROUP
    stopKey = 'stop'
    stopPort = concurrencyTestPort + 2
}



def getMongoHosts() {
    return project.hasProperty("mongo.hosts") ? getProperty("mongo.hosts") : "localhost"
}

def getHiveHost() {
    return project.hasProperty("hive.host") ? getProperty("hive.host") : "localhost:10000"
}

def getHdfsUrl() {
    return project.hasProperty("hdfs.url") ? getProperty("hdfs.url") : "hdfs://localhost:8020"
}

import org.gradle.api.plugins.jetty.internal.Monitor

concurrencyTestWar.doLast {
    /**
     * THIS IS A WORKAROUND! THE CURRENT VERSION OF THIS TASK DOESN'T START A WATCHER IN DAEMON MODE
     *
     * If starting the monitor fails, it may be because the jetty task was updated to fix this issue
     * When that happens, we shouldn't need the custom task any more
     *
     * http://issues.gradle.org/browse/GRADLE-2263
     */
    if (getStopPort() != null && getStopPort() > 0 && getStopKey() != null) {
        Monitor monitor = new Monitor(getStopPort(), getStopKey(), server.getProxiedObject());
        monitor.start();
    }
}

